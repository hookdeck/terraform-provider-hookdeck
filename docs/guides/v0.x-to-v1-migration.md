---
page_title: "Migrating from v0.x to v1.x"
description: "How to migrate from v0.x to v1.x of the Hookdeck Terraform Provider"
---

The Hookdeck API version `2025-01-01` introduces several breaking changes to support the new concepts of Source and Destination Types. Consequently, we made updates to the Hookdeck Terraform Provider to align with this new model.

This guide highlights the Hookdeck Terraform Provider breaking changes and identifies the changes you should make to your Terraform configuration to move to the v1.0 version.

## Breaking Changes

### `hookdeck_source` and `hookdeck_destination` changes

`hookdeck_source` and `hookdeck_destination` resources implement the Source and Destination Types model

- Added `type` property to the [Source](https://hookdeck.com/docs/api#sources) and [Destination](https://hookdeck.com/docs/api#destinations) objects, which can be set when creating or updating them.
- Added `config` property to contain all type-dependent configuration options, including authentication methods, custom responses, and HTTP method restrictions. See the [source object](https://hookdeck.com/docs/api#source-object) for more details on each Type configuration.

### `hookdeck_source` changes

- The Source `type` property defaults to `WEBHOOK` and has no required `config` property.
- The `custom_response`, `allowed_http_methods` properties have been moved to the Source to the `config` object for the supported `type`.
- The `verification` has been moved to the `config` object and the `verification.type` property is now `config.auth_type` and the `verification.config` property is `config.auth`.

### `hookdeck_destination` changes

- The Destination `type` property defaults to `HTTP` and requires a `config.url` property.
- The Destination properties `url`, `rate_limit`, `rate_limit_period`, `disabled_path_forwarding` and `http_method` have been moved to the `config` object for the supported `type`.
- The Destination `cli_path` property has been renamed to `path` and has been moved to the `config` object when `type` is `CLI`.
- When `type` is `CLI` the `rate_limit` and `rate_limit_period` properties are not supported.

### `hookdeck_source_verification` has been replaced with `hookdeck_source_auth`

- `auth_type` has been added to identify the type of authentication. It defaults to `WEBHOOK`. See the [Source object](https://hookdeck.com/docs/api#source-object) for all the supported values.
- `verification` is replaced with `auth` and `auth` is a `jsonencode` value. The value contents depend on the `auth_type`. See the [Source object](https://hookdeck.com/docs/api#source-object) for all the supported values.

## Example Migration

Here is a v0.x example:

```hcl
resource "hookdeck_source" "my_source" {
  name = "my_source"
}

resource "hookdeck_source_verification" "my_source_verification" {
  source_id = hookdeck_source.my_source.id
  verification = {
    basic_auth = {
      username = "username"
      password = "username"
    }
  }
}

resource "hookdeck_destination" "my_destination" {
  name = "my_destination"
  url  = "https://my.example.com/api"
  auth_method = {
    basic_auth = {
      username = "username"
      password = "password"
    }
  }
  rate_limit = {
    period = "concurrent"
    limit  = 10
  }
}
```

And the same example in v1.0 onwards:

```hcl
resource "hookdeck_source" "my_source" {
  name = "my_source"

  # new optional `type` property. Defaults to a value of `WEBHOOK`
  type = "HTTP"
}

# `hookdeck_source_auth` replaces `hookdeck_source_verification`
resource "hookdeck_source_auth" "my_source_auth" {
  source_id = hookdeck_source.my_source.id
  auth_type = "BASIC_AUTH"
  auth = jsonencode({
    username = "username"
    password = "password"
  })
}

resource "hookdeck_destination" "my_destination" {
  name = "my_destination"
  
  # new `type` property
  type = "HTTP"

  # new `config` property with all values set via `jsonencode`
  config = jsonencode({
    url       = "https://mock.hookdeck.com"
    auth_type = "BASIC_AUTH"
    auth = {
      username = "username"
      password = "password"
    }
    
    # `rate_limit` properties move out of object to match API
    rate_limit = 10
    rate_limit_period = "concurrent"
  })
}
```
